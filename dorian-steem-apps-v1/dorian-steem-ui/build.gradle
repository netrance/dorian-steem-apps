plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
}

apply from: '../build.dependencies.gradle'

android {
    compileSdk 33

    defaultConfig {
        applicationId "lee.dorian.steem_ui"
        minSdk 24
        targetSdk 33
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        freeCompilerArgs = ['-Xjvm-default=enable']
        jvmTarget = '1.8'
    }
    buildFeatures {
        dataBinding true
        viewBinding true
    }

    // Added to solve unit test failure. It is
    // java.lang.RuntimeException: Method getMainLooper in android.os.Looper not mocked.
    testOptions {
        unitTests.returnDefaultValues = true
    }
}

dependencies {
    // To refer to other modules
    implementation project(':dorian-steem-domain')
    implementation project(':dorian-steem-data')
    implementation project(":dorian-steem-test")

    // ext block is located in build.dependencies.gradle file.
    // androidx map is located in ext block.
    implementation androidx.core
    implementation androidx.appcompat
    implementation androidx.constraintlayout
    implementation androidx.lifecycle_livedata_ktx
    implementation androidx.lifecycle_viewmodel_ktx
    implementation androidx.navigation_fragment_ktx
    implementation androidx.navigation_ui_ktx
    implementation androidx.swiperefreshlayout

    // glide
    implementation glide.glide
    annotationProcessor glide.compiler

    // google
    implementation google.material

    // rx
    implementation rxjava.rxjava
    implementation rxjava.rxandroid

    // coroutines
    implementation coroutines.coroutines

    // for unit test
    testImplementation project(':dorian-steem-data')
    testImplementation others.junit
    testImplementation androidx.core_testing
    testImplementation coroutines.coroutines_test

    // for UI test
    androidTestImplementation androidx.junit
    androidTestImplementation androidx.espresso_core
}